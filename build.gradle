plugins {
	id "architectury-plugin" version "3.4-SNAPSHOT"
	id "dev.architectury.loom" version "1.7-SNAPSHOT" apply false
}

architectury {
	minecraft = rootProject.minecraft_version
}

subprojects {
	apply plugin: "dev.architectury.loom"

	dependencies {
		minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
		mappings "dev.architectury:yarn-mappings-patch-neoforge:${project.yarn_mappings}"
	}

	base {
		archivesName = rootProject.archives_base_name + '-' + project.name
	}

	jar {
		from(rootProject.file("LICENSE")) {
			rename { "${it}_${project.archives_base_name}"}
		}
	}
}

allprojects {
	apply plugin: "java"
	apply plugin: "architectury-plugin"
	apply plugin: "maven-publish"

	version = rootProject.mod_version
	group = rootProject.maven_group
	if ("false".equalsIgnoreCase(System.getenv("IS_RELEASE"))) {
		version += "-SNAPSHOT"
	}

	repositories {
		// Add repositories to retrieve artifacts from in here.
		// You should only use this when depending on other mods because
		// Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
		// See https://docs.gradle.org/current/userguide/declaring_repositories.html
		// for more information about repositories.
	}

	tasks.withType(JavaCompile) {
		options.encoding = "UTF-8"
		options.release = 21
		sourceCompatibility = "21"
		targetCompatibility = "21"
	}

	java {
		withSourcesJar()
	}

	publishing {
		// select the repositories you want to publish to
		repositories {
			maven {
				url = version.endsWith("SNAPSHOT") ? "https://maven.fallenbreath.me/snapshots" : "https://maven.fallenbreath.me/releases"
				credentials(PasswordCredentials) {
					username = 'fallen'
					password = System.getenv("FALLENS_MAVEN_TOKEN")
				}
				authentication {
					basic(BasicAuthentication)
				}
			}
		}
	}
}
